plugins {
    id "java"
    id "idea"
}

ext.api = project

group "com.github.mikucat0309"
version "1.0"

// Java version
sourceCompatibility = "1.11"
targetCompatibility = "1.11"

repositories {
    mavenCentral()
    jcenter()
}

// Source compiler configuration
tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xlint:all", "-Xlint:-path", "-parameters"]
    options.deprecation = true
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = "full" // Always print full stack trace if something goes wrong in the unit tests
        showStandardStreams = true
    }
}

// Fix problems caused by separate output directories for classes/resources in IntelliJ IDEA
idea {
    module {
        inheritOutputDirs = true
    }
}

dependencies {

    // Testing
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.6.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.6.2")
    testImplementation("org.hamcrest:hamcrest-library:2.2")
    testImplementation("org.mockito:mockito-core:3.2.4")

    // Logging
    implementation("org.slf4j:slf4j-log4j12:1.7.30")

    implementation("com.google.guava:guava:28.1-jre")
    implementation("org.apache.commons:commons-lang3:3.9")

    implementation("com.flowpowered:flow-math:1.0.3")

}

// JAR manifest configuration
jar {
    manifest {
        attributes("Main-Class": "com.github.mikucat0309.MeowMain")
    }
}

